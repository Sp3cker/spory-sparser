raw `
MauvilleCity_House1_MapScripts::
	.byte 0

MauvilleCity_House1_EventScript_RockSmashDude::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_HM_ROCK_SMASH, MauvilleCity_House1_EventScript_ReceivedRockSmash
	msgbox MauvilleCity_House1_Text_ImRockSmashDudeTakeThis, MSGBOX_DEFAULT
	giveitem ITEM_HM_ROCK_SMASH
	setflag FLAG_RECEIVED_HM_ROCK_SMASH
	setflag FLAG_HIDE_ROUTE_111_ROCK_SMASH_TIP_GUY
	msgbox MauvilleCity_House1_Text_ExplainRockSmash, MSGBOX_DEFAULT
	release
	end

MauvilleCity_House1_EventScript_ReceivedRockSmash::
	msgbox MauvilleCity_House1_Text_MonCanFlyOutOfSmashedRock, MSGBOX_DEFAULT
	release
	end


MauvilleCity_House1_EventScript_StatEditorDude::
	lock
	faceplayer
	goto_if_set FLAG_SYS_STAT_EDITOR_GET, MauvilleCity_House1_EventScript_ReceivedStatEditor
	msgbox MauvilleCity_House1_Text_ImStatEditorDudeTakeThis, MSGBOX_DEFAULT
	setflag FLAG_SYS_STAT_EDITOR_GET
	playfanfare MUS_OBTAIN_ITEM
	msgbox MauvilleCity_House1_Text_ObtainedStatEditor, MSGBOX_DEFAULT
	waitfanfare
	msgbox MauvilleCity_House1_Text_ExplainStatEditorStart, MSGBOX_DEFAULT
	goto_if_set FLAG_MIN_GRINDING_MODE, MauvilleCity_House1_EventScript_ExplainStatEditorInMGM
	goto MauvilleCity_House1_EventScript_ExplainStatEditorNotInMGM
	release
	end

MauvilleCity_House1_EventScript_ExplainStatEditorInMGM::
	msgbox MauvilleCity_House1_Text_ExplainStatEditorInMGM, MSGBOX_DEFAULT
	release
	end

MauvilleCity_House1_EventScript_ExplainStatEditorNotInMGM::
	msgbox MauvilleCity_House1_Text_ExplainStatEditorNotInMGM, MSGBOX_DEFAULT
	release
	end

MauvilleCity_House1_EventScript_ReceivedStatEditor::
	goto_if_set FLAG_MIN_GRINDING_MODE, MauvilleCity_House1_EventScript_ReceivedStatEditorInMGM
	goto MauvilleCity_House1_EventScript_ReceivedStatEditorNotInMGM
	release
	end

MauvilleCity_House1_EventScript_ReceivedStatEditorInMGM::
	msgbox MauvilleCity_House1_Text_ReceivedStatEditorInMGM, MSGBOX_DEFAULT
	release
	end

MauvilleCity_House1_EventScript_ReceivedStatEditorNotInMGM::
	msgbox MauvilleCity_House1_Text_ReceivedStatEditorNotInMGM, MSGBOX_DEFAULT
	release
	end

MauvilleCity_House1_Text_ImRockSmashDudeTakeThis:
	.string "Woohoo!\p"
	.string "I hear people call me the Rock Smash\n"
	.string "Guy, but I find that sort of degrading.\p"
	.string "I think I deserve a bit more respect,\n"
	.string "like maybe the Rock Smash Dude.\p"
	.string "Woohoo!\p"
	.string "Anyways, your Pokémon look pretty\n"
	.string "strong.\p"
	.string "I like that!\n"
	.string "Here, take this Hidden Machine!$"

MauvilleCity_House1_Text_ExplainRockSmash:
	.string "That HM contains Rock Smash.\p"
	.string "If you come across large boulders\n"
	.string "that block your path…\p"
	.string "Well, use that HM move and smash\n"
	.string "them right out of your way!\p"
	.string "Yes, sir! Smash rocks aside, I say!\n"
	.string "Woohoo!$"

MauvilleCity_House1_Text_MonCanFlyOutOfSmashedRock:
	.string "Oh, yes, if you smash a rock, a Pokémon\n"
	.string "could come flying out of hiding.\p"
	.string "Woohoo!$"

MauvilleCity_House1_Text_ImStatEditorDudeTakeThis:
	.string "Hello there.\p"
	.string "I'm not quite as outspoken as my\n"
	.string "colleague there, but I have something\l"
	.string "you may find useful.\p"
	.string "Here, take this device.$"

MauvilleCity_House1_Text_ExplainStatEditorStart:
	.string "I call that device the Stat Editor,\n"
	.string "{COLOR GREEN}{SHADOW LIGHT_GREEN}you can access it from your “L menu”.\p"
	.string "{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}It will allow you to edit the EVs or IVs\n"
	.string "of your Pokémon, depending on your\l"
	.string "current game mode.\p"
	.string "Hmm, let's see here…$"


MauvilleCity_House1_Text_ExplainStatEditorInMGM:
	.string "Since you have Minimal Grinding Mode\n"
	.string "enabled, you can't change your\l"
	.string "Pokémon's EVs with that device.\p"
	.string "You can edit IVs freely though.$"

MauvilleCity_House1_Text_ExplainStatEditorNotInMGM:
	.string "Since you have Minimal Grinding Mode\n"
	.string "disabled, you can edit both EVs\l"
	.string "and IVs freely with that device.$"

MauvilleCity_House1_Text_ReceivedStatEditorInMGM:
	.string "Remember, you can access the Stat\n"
	.string "Editor from your “L menu”.\p"
	.string "However, since you have\n"
	.string "Minimal Grinding Mode enabled, you\l"
	.string "can't change your Pokémon's EVs.\p"
	.string "You can edit IVs freely though.$"

MauvilleCity_House1_Text_ReceivedStatEditorNotInMGM:
	.string "Remember, you can access the Stat\n"
	.string "Editor from your “L menu”.\p"
	.string "Since you have Minimal Grinding\n"
	.string "Mode disabled, you can edit the\l"
	.string "EVs and IVs of your Pokémon freely.$"

MauvilleCity_House1_Text_ObtainedStatEditor:
	.string "Obtained the Stat Editor!$"
`


script MauvilleCity_House1_EventScript_HPChangerDude {
    lock
    faceplayer
	msgbox(HpChangerHelloCanChange, MSGBOX_YESNO)

	if(var(VAR_RESULT) == NO)
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	msgbox("Which Pokémon?")
	special(ChoosePartyMon)
	waitstate

	if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)

	if(var(VAR_RESULT) == SPECIES_EGG) {
		msgbox("I can't change the Hidden Power\nof an Egg.")
		release
		end
	}

	special(BufferMonNickname)
	msgbox("Which new Hidden Power type would\nyou like for {STR_VAR_1}?")
	dynmultipush("Fighting", TYPE_FIGHTING)
	dynmultipush("Flying", TYPE_FLYING)
	dynmultipush("Poison", TYPE_POISON)
	dynmultipush("Ground", TYPE_GROUND)
	dynmultipush("Rock", TYPE_ROCK)
	dynmultipush("Bug", TYPE_BUG)
	dynmultipush("Ghost", TYPE_GHOST)
	dynmultipush("Steel", TYPE_STEEL)
	dynmultipush("Fire", TYPE_FIRE)
	dynmultipush("Water", TYPE_WATER)
	dynmultipush("Grass", TYPE_GRASS)
	dynmultipush("Electric", TYPE_ELECTRIC)
	dynmultipush("Psychic", TYPE_PSYCHIC)
	dynmultipush("Ice", TYPE_ICE)
	dynmultipush("Dragon", TYPE_DRAGON)
	dynmultipush("Dark", TYPE_DARK)
	dynmultipush("Cancel", MULTI_B_PRESSED)
	// // setting initial selected to a non-existant ID will always have it point to the first option
	dynmultistack(0, 0, FALSE, 6, FALSE, TYPE_FIGHTING, DYN_MULTICHOICE_CB_NONE)

	if (var(VAR_RESULT) == MULTI_B_PRESSED) 
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	copyvar(VAR_0x8005, VAR_RESULT)
	bufferhptypeinfo(STR_VAR_2, STR_VAR_3, VAR_RESULT)
	msgbox(AreYouSureHPTypeChange, MSGBOX_YESNO)

	if(var(VAR_RESULT) == NO) 
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	msgbox("I shall begin at once.")
	fadescreen(FADE_TO_BLACK)
	special(ChangeMonIVsForHP)
	playfanfare(MUS_OBTAIN_ITEM)
	waitfanfare
	closemessage
	fadescreen(FADE_FROM_BLACK)
	msgbox(HPTypeChangedComeBackSoon)
	release
	end
}

script MauvilleCity_House1_EventScript_HPChangerNo {
    msgbox("I see. Come back if you change\nyour mind.")
    release
    end
}


script MauvilleCity_House1_EventScript_IVPerfecterDude {
    lock
    faceplayer
	msgbox(IVPerfecterGreeting, MSGBOX_YESNO)

	if(var(VAR_RESULT) == NO)
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	msgbox("Which Pokémon?")
	special(ChoosePartyMon)
	waitstate

	if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)

	if(var(VAR_RESULT) == SPECIES_EGG) {
		msgbox("I can't perfect the IVs\nof an Egg.")
		release
		end
	}

	special(BufferMonNickname)
	msgbox(IVPerfecterAreYouSure, MSGBOX_YESNO)

	if(var(VAR_RESULT) == NO) 
	{
		goto(MauvilleCity_House1_EventScript_HPChangerNo)
	}

	msgbox("I shall begin at once.")
	fadescreen(FADE_TO_BLACK)
	special(PerfectMonIVs)
	playfanfare(MUS_OBTAIN_ITEM)
	waitfanfare
	closemessage
	fadescreen(FADE_FROM_BLACK)
	msgbox(IVPerfecterComeBackSoon)
	release
	end
}


text HpChangerHelloCanChange
{
	"Hello, I am the Hidden Power type\n"
	"changer.\p"
	"You can choose a Pokémon and I can\n"
	"quickly modify its IVs to change\l"
	"the type of its Hidden Power.\p"
	"Would you like me to change the type of\n"
	"one of your Pokémon's Hidden Power?"
}

text HpChangerNotInMgm
{
	"Oh, it looks like you are not in\n"
	"Minimal Grinding Mode.\p"
	"I can only change the Hidden Power\n"
	"type of a Pokémon whose IVs have been\l"
	"perfected.\p"
	"That limitation will be in place until\n"
	"you have all 8 Gym Badges."
}


text AreYouSureHPTypeChange
{
	"So you want me to change\n"
	"{STR_VAR_1}'s Hidden Power type\l"
	"to {STR_VAR_2}?\p"
	"This will modify {STR_VAR_1}'s\n"
	"IVs to be the following:\p"
	"HP   Atk   Def   SpAtk   SpDef   Spd\n"
	"{STR_VAR_3}"
}

text HPTypeChangedComeBackSoon
{
	"{STR_VAR_1}'s Hidden Power type\n"
	"has been changed to {STR_VAR_2}.\p"
	"Come back if you need me to change\n"
	"the Hidden Power type of\l"
	"another Pokémon."
}

text IVPerfecterGreeting
{
	"Hello, I am the IV Perfecter,\n"
	"I can perfect one of your\l"
	"Pokémon's IVs.\p"
	"Would you like me to perfect one of\n"
	"your Pokémon's IVs?"
}

text IVPerfecterAreYouSure
{
	"So you want me to perfect\n"
	"{STR_VAR_1}'s IVs?"
}

text IVPerfecterComeBackSoon
{
	"{STR_VAR_1}'s IVs have been\n"
	"perfected.\p"
	"Come back if you need me to perfect\n"
	"the IVs of another Pokémon."
}

